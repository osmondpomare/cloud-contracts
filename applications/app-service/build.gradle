apply plugin: 'org.springframework.boot'
apply plugin: 'org.springframework.cloud.contract'
apply plugin: 'org.springframework.boot'

dependencies {
	implementation project(':reactive-web')
    implementation project(':model')
    implementation project(':usecase')
    implementation 'org.springframework.boot:spring-boot-starter'
    runtimeOnly('org.springframework.boot:spring-boot-devtools')
    testImplementation 'com.tngtech.archunit:archunit:1.4.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
    testImplementation 'io.rest-assured:spring-web-test-client'
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

contracts {
    if (project.name == 'app-service') {
        contractsDslDir = project.file("${project.projectDir}/src/test/java/contracts")
        baseClassForTests = 'contracts.BaseTestClass'
    } else {
        failOnNoContracts = false
    }
    testMode = 'WebTestClient'
    testFramework = 'JUNIT5'
    basePackageForTests = 'contracts'
}

tasks.register('explodedJar', Copy) {
    with jar
    into layout.buildDirectory.dir("exploded")
}

jar {
    // To disable the *-plain.jar
    enabled = false
}

bootJar {
    // Sets output jar name
    archiveFileName = "${project.getParent().getName()}.${archiveExtension.get()}"
}
